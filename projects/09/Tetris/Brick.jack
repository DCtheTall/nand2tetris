/** Brick class provides an abstraction for the different types of bricks used in the game. */
class Brick {
    static int nTypes;

    field int type;
    field int orientation;

    constructor Brick new(int aType) {
        let type = aType;
        let orientation = 0;
        return this;
    }

    method void dispose() {
        do Memory.deAlloc(this);
        return;
    }

    method bool isIBlock() { return type = 0; }

    method bool isLBlock() { return type = 1; }

    method bool isJBlock() { return type = 2; }

    method bool isOBlock() { return type = 3; }

    method bool isSBlock() { return type = 4; }

    method bool isZBlock() { return type = 5; }

    // T-block type = 6

    method int getOrientation() { return orientation; }

    method int shapeSize() {
        if (isIBlock()) {
            return 4;
        }
        if (isOBlock()) {
            return 2;
        }
        return 3;
    }

    method Array shape() {
        var int size;
        var int sizeSquared;
        var Array result;
        var int i;

        let size = shapeSize();
        let sizeSquared = size * size;
        let result = Array.new(sizeSquared);

        let i = 0;
        while (i < sizeSquared) {
            let result[i] = indexInShape(i);
            let i = i + 1;
        }
        return result;
    }

    method bool indexInShape(int i) {
        if (isIBlock()) {
            return MathExt.inRange(i, 4, 8);
        }
        if (isLBlock()) {
            return (i = 0) | MathExt.inRange(i, 3, 6);
        }
        if (isJBlock()) {
            return (i = 2) | MathExt.inRange(i, 3, 6);
        }
        if (isOBlock()) {
            return true;
        }
        if (isSBlock()) {
            return MathExt.inRange(i, 1, 3) | MathExt.inRange(i, 3, 5);
        }
        if (isZBlock()) {
            return MathExt.inRange(i, 0, 2) | MathExt.inRange(i, 4, 6);
        }
        // T-block
        return (i = 1) | MathExt.inRange(i, 3, 6);
    }

    method void rotate() {
        let orientation = MathExt.mod(orientation + 1, 4);
        return;
    }

    method void undoRotate() {
        let orientation = MathExt.mod(orientation - 1, 4);
        return;
    }

    method Array rotatedShape() {
        var Array curShape;
        var int i;
        var int size;

        let curShape = shape();
        if (isOBlock()) {
            return curShape;
        }

        let size = shapeSize();
        let i = 0;
        while (i < orientation) {
            let curShape = Brick.rotateShape(curShape, size);
            let i = i + 1;
        }

        return curShape;
    }

    function void init() {
        let nTypes = 7;
        return;
    }

    // TODO fix
    function Brick randomBrickType() {
        return Brick.new(Random.randRange(nTypes));
    }

    function int maxShapeSize() { return 4; }

    function Array rotateShape(Array prevShape, int size) {
        var int sizeSquared;
        var Array result;
        var int i;
        var int newIdx;

        let sizeSquared = size * size;
        let result = Array.new(sizeSquared);

        let i = 0;
        while (i < sizeSquared) {
            let newIdx = (MathExt.mod(i, size) * size) + (size - 1 - (i / size));
            let result[newIdx] = prevShape[i];
            let i = i + 1;
        }

        return result;
    }
}
