/** Random number generator, from https://gist.github.com/ybakos/7ca67fcfd07477a9550b */
class Random {
    static int seed;
    
    /** Set the random seed */
    function void setSeed(int aSeed) {
        let seed = aSeed;
        return;
    }

    /** return a random number in the range 0..32767 */
    function int rand() {
        let seed = seed + 20251;
        if (seed < 0) {
            let seed = seed - 32767 - 1;
        }
        return seed;
    }

    /** return a random number in the range 0..range */
    function int randRange(int range) {
        var int mask;
        var int ret;
        let mask = 1;
        while (mask < range) {
            let mask = mask * 2 + 1;
        }
        let ret = Random.rand() & mask;
        while (ret > range) {
            let ret = Random.rand() & mask;
        }
        return ret;
    }
}