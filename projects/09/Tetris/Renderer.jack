/** Renderer class is a static class that handles rendering game state to screen */
class Renderer {
    static int gridTop;
    static int gridLeft;
    static int cellSize;
    static int queueLeftMargin;
    static int scoreCursorRow;
    static int scoreCursorCol;

    function void init() {
        let gridTop = 6;
        let gridLeft = 50;
        let cellSize = 12;
        let queueLeftMargin = 20;
        let scoreCursorRow = 2;
        let scoreCursorCol = 35;
        return;
    }

    function void renderGameStart(GameState state) {
        var Grid grid;
        var int gridRight;
        var int gridBottom;

        let grid = state.getGrid();
        let gridRight = gridLeft + (grid.getWidth() * cellSize);
        let gridBottom = gridTop + (grid.getHeight() * cellSize);

        // Draw the outline of the grid
        do Screen.setColor(true);
        do Screen.drawLine(gridLeft, gridTop, gridRight, gridTop);
        do Screen.drawLine(gridLeft, gridTop, gridLeft, gridBottom);
        do Screen.drawLine(gridLeft, gridBottom, gridRight, gridBottom);
        do Screen.drawLine(gridRight, gridTop, gridRight, gridBottom);

        do Renderer.renderState(state);
        // do grid.dispose();
        return;
    }

    function void renderState(GameState state) {
        var Grid grid;
        var int queueLeft;
        let grid = state.getGrid();
        let queueLeft = gridLeft + (grid.getWidth() * cellSize) + queueLeftMargin;
        do Renderer.renderBrickQueue(state, queueLeft);
        do Renderer.renderScore(state);
        do Renderer.renderFallingBrick(state.getFallingBrick());
        return;
    }

    function void renderBrickQueue(GameState state, int queueLeft) {
        var int queueRight;
        var Grid grid;
        var int queueBottom;

        var int i;
        var BrickQueue queue;
        var int qSize;
        var Brick brickType;
        var int brickTypeSize;
        var int brickLeft;
        var int brickTop;

        // Draw a white rectangle over the current queue.
        let queueRight = queueLeft + (Brick.maxShapeSize() * cellSize);
        let grid = state.getGrid();
        let queueBottom = gridTop + (grid.getHeight() * cellSize);
        do Screen.setColor(false);
        do Screen.drawRectangle(queueLeft, gridTop + cellSize, queueRight, queueBottom);

        // Render each brick in the queue.
        let i = 0;
        let queue = state.getQueue();
        let qSize = queue.getSize();
        while (i < qSize) {
            let brickType = queue.getBrickTypeAtIndex(i);
            let brickTypeSize = brickType.shapeSize();
            let brickLeft = queueLeft + ((cellSize * (Brick.maxShapeSize() - brickTypeSize)) / 2);
            let brickTop = cellSize + gridTop + (Brick.maxShapeSize() * i * cellSize);
            do Renderer.renderBrick(brickType, brickLeft, brickTop);
            let i = i + 1;
        }
        return;
    }

    function void renderBrick(Brick brick, int x, int y) {
        var int size;
        var int sizeSquared;
        var Array shape;
        var int i;
        var int curx;
        var int cury;

        let size = brick.shapeSize();
        let sizeSquared = size * size;
        let shape = brick.rotatedShape();

        let i = 0;
        while (i < sizeSquared) {
            if (shape[i]) {
                let curx = x + (MathExt.mod(i, size) * cellSize);
                let cury = y + ((i / size) * cellSize);
                do Renderer.renderBrickCell(curx, cury);
            }
            let i = i + 1;
        }
        do shape.dispose();
        return;
    }

    function void renderBrickCell(int x, int y) {
        if (y < gridTop) {
            return;
        }
        do Screen.setColor(true);
        do Screen.drawRectangle(x, y, x + cellSize, y + cellSize);
        do Screen.setColor(false);
        do Screen.drawLine(x, y, x + cellSize, y);
        do Screen.drawLine(x, y, x, y + cellSize);
        return;
    }

    function void renderScore(GameState state) {
        do Output.moveCursor(scoreCursorRow, scoreCursorCol);
        do Output.printString("              ");
        do Output.moveCursor(scoreCursorRow, scoreCursorCol);
        do Output.printString("Score: ");
        do Output.printInt(state.getScore());
        return;
    }

    function void renderFallingBrick(FallingBrick fallingBrick) {
        var int x;
        var int y;

        if (~fallingBrick.isSpawned()) {
            return;
        }
        let x = gridLeft + (fallingBrick.getX() * cellSize);
        let y = fallingBrick.getY() * cellSize - gridTop;
        do Renderer.renderBrick(fallingBrick.getBrick(), x, y);
        return;
    }
}
